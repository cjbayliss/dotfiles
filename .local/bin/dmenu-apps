#!/usr/bin/env nu

# custom dmenu
def dmenu [] {
  if ($env.XDG_SESSION_TYPE == 'wayland') {
    tofi
  } else {
    ^dmenu -i -fn 'Iosevka-11.5:semibold' -nb '#000' -nf '#fff' -sb '#000' -sf '#b6a0ff'
  }
}

# function to generate the application list
def generate_app_list [] {
  $env.XDG_DATA_DIRS | split row ':' | append '~/.local/share'
  | each {||
    if ($'($in)/applications' | path exists) {
      glob $'($in)/applications/**'
    }
  }
  | flatten | wrap name | where name =~ '\.desktop' | get name
  | each {||
    open $in
    | split row -r '\n'
    | find -r '^Name=|^Exec='
    | get 0 1
    | str replace 'Name=' 'name,'
    | str replace 'Exec=' 'command,'
    | str replace -r ' -- %.| %.' ''
    | to text | from csv | transpose | headers
  }
  | flatten | uniq | select name command | sort | append [[name,command];[poweroff,poweroff],[reboot,reboot]]
}

def main [] {
  let $apps = generate_app_list
  let $selection = ($apps | get name | to text | dmenu | str replace -r '\n' '')

  if not ($selection | is-empty) {
    sh -c $'($apps | where name == $selection | get command | to text) &'
  }
}
